<#> strict(none)
pattern { bgm {
	1  simp.2.stage
} }
phase 0
	paction 0
		shift-phase-to 7
		
!{ mw(source, t1, t2, loc1, loc2, hp, fire)
gsr { 
	root %source
} summon null 
	imovewrap %t1 %loc1 %t2 %loc2 
		%fire
	{ hp %hp }
!}
!{ mw12(source, loc1, loc2, hp, fire)
$mw(%source, 1, 2, %loc1, %loc2, %hp, %fire)
!}

!{ 1f(f, t, pr, pp)
paction 0 :2
	async gcircle-red/w <> gcr3 %f %t <18c> {
		while onscreen loc
		sfx x-fire-burst-1
	} gsr2c 18 {
		%pr
	} s tprot cx 2
	async gcircle-pink/w <> gcr3 * 2 %f  %t <6c> {
		while onscreen loc
	} gsr2c 6 {
		%pp
	} gsr2 6 <4> {
	} s tprot cx 2
!}

!{ 1(source, loc1, loc2, f, t)
sync fairy4 <> $mw12(%source, %loc1, %loc2, 6000, $1f(%f, %t, noop, noop))
!}

!{ 1c(f, t)
$1(cy 5, cy 2, cy 5, %f, %t)
!}
!{ 1l(f, t)
$1(cxy -3 5, cxy -3 2, cxy -6 1, %f, %t)
!}
!{ 1r(f, t)
$1(cxy 3 5, cxy 3 2, cxy 6 1, %f, %t)
!}

!{ 4(source, loc1, loc2, f, t, fire)
async fairy3 <> gcr3 %f %t <> {
	p this
} $mw12(%source, %loc1, %loc2, 600, >> 0.5 %fire)
!}
!{ 4.1(source, loc1, loc2, f, t)
$4(%source, %loc1, %loc2, %f, %t, 
	async arrow-red/ <> gir { sfx x-fire-burst-2 } gcr2 8 6 <> {
		target a Lplayer
		p this
	} s tprot (lerp3 0.2 0.4 1 1.6 t)
		px + 1 * 0.4 p
		px 0.5
		px - 4 * 0.1 p
)
!}
!{ 4.3(source, loc1, loc2, f, t)
$4(%source, %loc1, %loc2, %f, %t, 
	async arrow-yellow/ <> gir { sfx x-fire-burst-2 } gcr2 8 6 <> {
		p this
	} gsr {
		target a Lplayer
	} s tprot * (lerp 0.2 0.4 t 2 1) px + 1 * 0.4 p
)
!}

##1
<!> stage
phase 32 #{
	paction 0
		$1c(/ 40 dh, 8s)
		wait 9
	paction 0
		$1l(/ 60 dh, 8s)
		$4.1(cxy 3 5, pxy + 1 softmod 2.4 * 0.8 p 1, cxy 6 0, /i dh 90, 10s)
		wait 8
	paction 0
		$1r(60, 8s)
		$4.3(cxy -3 5, pxy - -1 softmod 2.4 * 0.8 p 1, cxy -6 0, /i dh 120, 10s)
		wait 8
#}


!{ 5f1(f, t)
async arrow-pink/w <> gcr3 %f %t <> {
	sfx x-fire-burst-1
	start a0 =f angleto cy -2
	postloop a0 +=f 180h
} gsr2c (* dh 40) {
} s polar
	* regpoly 1 5 &a
		smsht -0.4 1 (* 2.5 t) (* 1.5 t)
	&a0
!}

!{ 5f2(s, f, t)
async arrow-blue/w <> gcr3 %f %t <> {
	sfx x-fire-burst-1
	start a0 =f angleto cy -2
} gsr2c (* dh 40) {
} s polar
	* regpoly 1 5 &a
		smsht3 -0.4 1 0.4 2 (* 2 t) 0 (* 1.3 t)
	+ &a0 * %s smsht3 0.01 1 -0.01 2 0 * 180 t * 10 t
!}

!{ 12(x0, d, ct, s, fire)
gsr2 %ct <> {
	root pxy * %s 6 5
	p this
} summon null
	move-wrap 1 (pxy * %s (+ %x0 * %d p) 2.5) 2 pxy * %s 6 -5
		%fire
	{ hp 900 }
!}

!{ 12.1(s, f, t)
$12(2.5, 0.5, 3, %s,
	async gpather-yellow/b <> gcr3 %f %t <> {
		} gsr {
			target a Lplayer
			sfx x-fire-tech-6
		} pather 1 0.5 tprot px lerpt3 (0.2 0.4 0.7 1.1) 6 1 8 { }
)
!}

##3
<!> stage
phase 26 #{
	paction 0
		sync fairy3 <> $mw12(cy 5, cy 2, cy 5, 3000, $5f1(/i dl 50, 5s))
		sync fairy3 <> $mw12(cxy -3 5, cxy -3 1, cxy -6 0, 3000, $5f2(1, /i dl 120, 4s))
		sync fairy3 <> $mw12(cxy 3 5, cxy 3 1, cxy 6 0, 3000, $5f2(-1, /i dl 120, 4s))
		wait 7
	paction 0
		sync fairy3 <> $mw12(cy 5, cy 2, cy 5, 7000, $5f1(/i dl 70, 14s))
		sync fairy3 <> $mw12(cxy -3 5, cxy -3 1, cxy -6 0, 5000, $5f2(1, /i dl 180, 12s))
		sync fairy3 <> $mw12(cxy 3 5, cxy 3 1, cxy 6 0, 5000, $5f2(-1, /i dl 180, 12s))
		_ 4
			sync fairy2 <> $12.1(1, /i dl 120, 4.8s)
		_ 10
			sync fairy2 <> $12.1(-1, /i dl 120, 4.8s)
		wait 16
#}

!{ 5f3(color, f, t)
async %color <> gcr3 %f %t <> {
	sfx x-fire-burst-1
} gsr {
	start a0 =f angleto Lplayer
} gsr2c * dl 30 {
} s offset
	rx 
		* regpoly 1 5 &a
			smsht -2 1 (* 3 t) 0
		&a0
	rx 
		smsht 1 1.2 0 * 2 t
		&a0
!}

##4
<!> stage
phase 20 #{
	paction 0
!!{ f 180
		sync fairy3 <> $mw12(cxy -3 5, cxy -3 1, cxy -6 0, 9000, $5f3(arrow-blue/w, $f, 15s))
		_ * 0.5f $f
			sync fairy3 <> $mw12(cxy 3 5, cxy 3 1, cxy 6 0, 9000, $5f3(arrow-teal/w, $f, 15s))
		async fairy1 <> gcr {
			wt	240 _
			for	17s
			frv2 rx * 1 mod 1 * 1h t
			p this
		} gsr2 14 rx - 1.5 * 0.3 dh {
			center
		} $mw12(cy 4, pxy x 2, pxy x 4, 700, 
			async gpather-*/ <> gcr {
				sfx x-fire-tech-8
				colorf { purple yellow red pink } p
			} pather 1 1 tprot py -2 { }
		)
#}

##5
<!> stage
phase 18 #{
	paction 0
		async fairy1 <> gcr3 240 14s <> {
			p this
		} gsr2 20 <0.6;:> {
			center
			preloop idx =f t
		} $mw(cy 4, 1, 2, pxy x 2, pxy x 4, 600, 
			>> 0.5 async sakura-*/ <> gir {
				delay rand 0 120
				sfxif x-fire-burst-2 divby 3 &idx
				colorf { purple red pink } p
			} gcr2d dl 8 8 <> {
				p this
			} s tprot py + -0.8 * -0.3 pdl
		)
		async fairy2 <> gcr3 360 15s <> {
			preloop lr =f pm1 t
		} $12.1(&lr, /i dl 120, 3.2s)
#}

##6. Mid-boss
<!> midboss
phase 0
	saction 0
		boss simp.kaguya-fake
		shift-phase

##7
<!> stage
phase 50 #{
!!{ t1 27s
	paction 0
		gtr {
			wt	/i (min 1 ^ dl 0.6) 360 _
			for	$t1
			p this
		} saction 0 :2
			crosshair crosshair Lplayer 0 2 ploc p
			sync gpather-*/b <> gsr2c * sqrt dl 12 {
				sfx x-fire-tech-6
				root @ ploc p
			} gsr2 2 <> {
				p this
				color { red pink }
			} pather 0.5 0.5 tprot lerpt 0.8 1.2 px (lerpt 0 1 1 2) rx 2 * pm1 p 40 { }
			
		async crow <> gcrf lerpt 0 8 360 240 _ <> {
			delay	+ $t1 2s
			p this
			root pxy * 4 pm1 p 5
		} summon null imovewrap 1 (pxy * 4 rangemod 1 * 2h p 2) 2 (pxy * pm1 p 6 1)
			paction 0.5 :2
				sync lightning-red/w <180> gsr2 0 <> { ##this is too mcuh...
					sfx x-lightning
					target a Lplayer
				} pather 1 0.5 tpnrot
						* lerp3 (0.0 0.2 1.9 2.3 t) 5 0.7 4
							truerotatelerprate 200 rotify cx 2
								ss 1.7 vhome 2 Lplayer
						{ }
				async sakura-green* <> gcr {
					sfx x-crow
				} gsr2c * dl 21 {
					start a0 =f angleto Lplayer
				} gsr2 2 <> {
					p this
					color { / /b }
				} s tprot rx
					* regpoly 1 3 &a
						if (= p 0)
							lerpt 0.5 1.2 3 2
							lerpt 0.5 1.5 2 -3.5
					&a0
			{ hp 800 }
		
		async crow <-4;-5:> gcr2 * 2s 1p _ <180> {
			delay 6s
			p this
		} summon tprot cy 1
			paction 0 :2
				async gdlaser-yellow/b <-2;:> iparent this gcr {
				} laser null 1 0.5 { repeat sfx2 `` x-laser-on }
				async fireball-blue/b <> gcr2 3s _ <> {
					while & > p 1 onscreen loc
					sfx	x-crow
					delay 2s
				} gsr2 * dl 6 (angle /i dl 24) {
					center
					face d
					target a Lplayer
				} s tprot px lerpt 0.5 1 1.5 2.5
			{ hp 2700 }

#}


##8
<!> stage
phase 20 #{
	paction 0
		sync danger <-3;-6:> gsr2 2 <6;:> { } summonsup 
			tprot lerp3 1 1.5 2 2.5 t cy 2.2 zero cy -2
			wait
		async crow <-4;-5:> gcr2 360 _ <> {
			delay 3s
		} gsr2 2 <8;:> {
			p this
		} summon tprot py lerpt 0 2 6 3
			paction 0 :2
				async x-fire-tech-8 <> gcr2 24 5 <> { } sfx
				async gpather-*/b <> gcr {
					wt	/i dl 4 _
					while & > y -4 onscreen loc
					rpp	angle /i dl * pm1 p + 24.5 * 12.62 p
					colorf { yellow pink } p
				} pather 0.5 0.5 tprot cx 3 { }
				{ hp 1000 }
		async crow <-6;3:> gcr2 /i (min 1.5 dl) (lerpt 0 4 360 240) _ <> {
			p this
			sfx x-crow
		} gsr2 3 <1;1:> {
			preloop wc =f t
		} gsr {
			start rv2.rx *=f mp1 p
		} summon tpnrot 
				* lerpt 0.5 1.5 0.4 1
					ss0 vhome 5 Lplayer
			async ellipse-red/w <180> gcr2 24 _ <> { 
				delay 60
				sfxif x-fire-burst-1 = &wc 0
				while onscreen loc
				face v
			} gsr2 7 <;0.2:> {
				center
			} s tprot px lerpt 1 2.3 2 -8
			{ hp 200 }
#}

## 9. End-boss
<!> endboss
phase 0
	saction 0
		boss simp.yukari
		shift-phase



